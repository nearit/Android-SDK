buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
    dependencies {
        classpath 'com.novoda:gradle-build-properties-plugin:0.3'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.novoda.build-properties'

buildProperties {
    localProperties {
        file project.file("local.properties")
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.0'
    defaultConfig {
        applicationId "com.nearit.sample"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        resValue "string",
                "near_api_key",
                (buildProperties.localProperties['nearit_api'] | buildProperties.env['NEAR_API']).string

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    flavorDimensions "environment"
    productFlavors {
        staging {
            dimension "environment"
            applicationIdSuffix ".staging"
            versionNameSuffix "-staging"
        }
        production {
            dimension "environment"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    // implementation 'it.near.sdk:nearit:2.12.3'
    implementation 'it.near.sdk:nearit-ui:2.11.9'
    implementation(project(':nearit'))

    implementation 'androidx.appcompat:appcompat:1.0.2'
}

apply plugin: 'com.google.gms.google-services'
